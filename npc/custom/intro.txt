// these scripts hopefully account for everything required for the renewal
//  starting point, since I like it way more than the pre-re starting point
//
// scripts grabbed from
//   npc\re\jobs\novice\academy.txt
//   npc\re\mobs\int_land.txt
//   npc\re\warps\cities\izlude.txt

int_land,0,0,0,0	monster	Poring	1002,25,0,0,0

//= iz_int(Intro) & int_land(Secluded Island) ================
iz_int,27,30,0	script	noviship#room1-1	WARPNPC,2,2,{
	end;

OnTouch:
	warp(strnpcinfo(NPC_MAP), 51, 30);
	end;

OnInit:
	questinfo(QTYPE_QUEST, 1);
	setquestinfo(QINFO_JOB, Job_Novice);
	setquestinfo(QINFO_QUEST, 21001, 1);
	end;
}

iz_int,47,30,3	script	noviship#room1-2	WARPNPC,2,2,{
	end;

OnTouch:
	@display = 0;
	warp(strnpcinfo(NPC_MAP), 22, 30);
	end;
}

iz_int,56,15,0	script	noviship	WARPNPC,2,2,{
	end;

OnTouch:
	savepoint("int_land"+strnpcinfo(NPC_NAME_HIDDEN), 77, 101);
	warp("int_land"+strnpcinfo(NPC_NAME_HIDDEN), 85, 107);
	end;
}

int_land,49,57,0	script	noviship#izlude	WARPNPC,2,2,{
	end;

OnTouch:
	mes("^4d4dffOnce you left this island there is no way back.");
	mes("Are you sure you want to go directly to Izlude?^000000");
	next();
	mes("^4d4dffIf you do, the quest got deleted from your Quest Log.^000000");
	next();
	if (select("Do not go to Izlude yet", "Sail to Izlude!") == 1) {
		mes("[Sailor]");
		mes("You want to stay here longer?");
		mes("Good, then I wait until you're ready~");
		close();
	}
	mes("[Sailor]");
	mes("Lets head toward Izlude!");
	close2();
	if (questprogress(21001))
		erasequest(21001);
	if (questprogress(21002))
		erasequest(21002);

	.@map$ = "izlude";
	if (strnpcinfo(NPC_NAME_HIDDEN) != "izlude")
		.@map$ = .@map$ + strnpcinfo(NPC_NAME_HIDDEN);

	warp(.@map$, 196, 209);
	end;
}

//== Starting Point  (iz_int) =================================
iz_int,18,26,0	script	startpoint	HIDDEN_WARP_NPC,1,1,{
OnTouch:
	if (!izintspawn) {
		navigateto("int_land", NAV_NONE, 1); // individual map name not required
		izintspawn = 1;
	}
	end;
}

//== Wounded Swordsman (iz_int) =================================
iz_int,56,32,3	script	Wounded Swordsman#izint	4_TOWER_01,8,8,{
	if (!@display) {
		classchange(4_TOWER_02, 0);
		npctalk(_("Ohhhhhhh...."));
		@display = 1;
		end;
	}
	switch(questprogress(21001)) {
	case 0:
		mes("[Wounded]");
		mes("Wow! Thank you a lot!");
		mes("I don't know how this happend to our ship");
		mes("but we should go to see the captain.");
		next();
		mes("[Wounded]");
		mes("... ohh, it seems my body is injured too much.");
		mes("Maybe you can go without me?");
		next();
		setquest(21001);
		break;
	case 1:
		mes("[Wounded]");
		mes("Luckily the water does not occupy the ship.");
		mes("I wait here, you should go and see the captain.");
		next();
		mes("[Wounded]");
		mes("I'm fine! I'm fine!");
		mes("Don't worry about me, you should really go to the captain!");
		next();
		break;
	case 2:
		break;
	}
	cutin("tutorial02", 4);
	mes("^4d4dff!- Information -!^000000");
	mes("NPC Quest Received.");
	mes("^4d4dffQuestinfo Shortcut is Alt + U^000000 You can check your quest status there anytime.");
	close2();
	cutin("", 255);
	end;

OnInit:
	questinfo(QTYPE_QUEST, 1);
	setquestinfo(QINFO_JOB, Job_Novice);
	setquestinfo(QINFO_QUEST, 21001, 1);
	end;

OnTouch_:
	if (@cd + 15 > gettimetick(2))
		end;
	@cd = gettimetick(2);
	npctalk(_("Urgh... please... help me..."));
	sleep(7000);
	npctalk(_("In order to talk with me.. click my body.."));
	end;
}

int_land,58,69,5	script	Sailor#int_land	4W_SAILOR,{
	if (!questprogress(21002) || questprogress(21002) == 2) {
		mes("[Sailor]");
		mes("The ship is heading to Izlude soon~!");
		mes("I'll let you know when we set sail~!");
		close();
	} else {
		if (countitem(Wood) >= 2) {
			delitem(Wood, countitem(Wood));
			completequest(21002);
			mes("[Sailor]");
			mes("Unbelievable, perfect! Any chance you want to join my crew?");
			mes("Enough talking!!");
			mes("Come on, we're ready to set sails!");
			mes("Thank you so much!");
			next();
			mes("[Sailor]");
			mes("If you want to sail with us to Izlude, jump on board!");
			getexp(600, 0);
			specialeffect(EF_HEAL2, AREA, playerattached());
			heal(9999, 0);
			specialeffect(EF_INCAGILITY, AREA, playerattached());
			sc_start(SC_INC_AGI, 240000, 10);
			specialeffect(EF_BLESSING, AREA, playerattached());
			sc_start(SC_BLESSING, 240000, 10);
			next();
			mes("[Sailor]");
			mes("Oh, and take thos ^4d4dffMagnifier^000000.");
			mes("When you hunt monsters you can find ^4d4dffunidentified items^000000.");
			getitem(Spectacles, 5);
			next();
			mes("[Sailor]");
			mes("Using a Magnifier will identify the item.");
			next();
			mes("[Sailor]");
			mes("Maybe you have already some unidentified items from your fights with those porings?");
			close();
		}
		mes("[Sailor]");
		mes("These Porings stole my materials to repaid the ship. Do you help me to get them back?");
		mes("There are plenty of Porings on this Island.");
		next();
		mes("[Sailor]");
		mes("I really appreciate it if you help me.");
		mes("Apparently I am not allowed to move or discover this island.");
		mes("Thats why I ask you~");
		close();
	}

OnInit:
	questinfo(QTYPE_QUEST, 1);
	setquestinfo(QINFO_JOB, Job_Novice);
	setquestinfo(QINFO_QUEST, 21002, 1);
	end;
}

//== Captain Carocc (int_land) =================================
int_land,78,103,5	script	Captain Carocc#int_land	4_M_REINDEER,{
	switch(questprogress(21002)) {
	case 0:
		mes("[Captain Carocc]");
		mes("There are still people in the cabins?!");
		mes("At least you are save.");
		mes("Are you alright?");
		next();
		if (select("Im alright, but others need help.", "I think I am the last?") == 1) {
			mes("[Captain Carocc]");
			mes("There are more people left?");
			mes("I will send a rescue team to them");
			mes("Thank you for your report");
		} else {
			mes("[Captain Carocc]");
			mes("Alright, thats good.");
			mes("The storm came unexpected but I never thought it hit us that hard.");
		}
		next();
		mes("[Captain Carocc]");
		mes("At least, most people are already in safety.");
		next();
		mes("[Captain Carocc]");
		mes("Look at the trees there, ^4d4dfffollow them to the southwest^000000 to find a new ship.");
		mes("The Ship will take you to ^4d4dffIzlude^000000, our original destination.");
		next();
		mes("[Captain Carocc]");
		mes("Ah! Speaking of it, those ^4d4dffPorings^000000 on this island drop several material we could need for reparation.");
		next();
		mes("[Captain Carocc]");
		mes("^4d4dffKill the Porings and bring 2 Lumber to the southern crew^000000.");
		next();
		mes("[Captain Carocc]");
		mes("It is hard, but you look tough enough for this task.");
		if (questprogress(21001) == 1)
			completequest(21001);
		specialeffect(EF_HEAL2, AREA, playerattached());
		heal(9999, 0);
		specialeffect(EF_INCAGILITY, AREA, playerattached());
		sc_start(SC_INC_AGI, 240000, 10);
		specialeffect(EF_BLESSING, AREA, playerattached());
		sc_start(SC_BLESSING, 240000, 10);
		getexp(15, 0);
		setquest(21002);
		next();
		mes("[Captain Carocc]");
		mes("Speaking of Porings, you have to equip your gears.");
		mes("You should have a^4d4dffweapon and an armor^000000 equipped.");
		next();
		cutin("tutorial03", 4);
		mes("!- Information -!");
		mes("^4d4dffYou can equip gear or use items by simply double click them.");
		mes("Item window can be opened by pressing Alt + E or using the buttons.^000000");
		next();
		mes("!- Information -!");
		mes("^4d4dffEquipped Items can be seen by pressing ALT + Q.");
		mes("Either double click or drag & drop them.^000000");
		next();
		cutin("", 255);
		mes("[Captain Carocc]");
		mes("Kill Porings and bring the Lumber to the crew.");
		mes("Good Luck!");
		close();
	case 1:
		mes("[Captain Carocc]");
		specialeffect(EF_HEAL2, AREA, playerattached());
		heal(9999, 0);
		specialeffect(EF_INCAGILITY, AREA, playerattached());
		sc_start(SC_INC_AGI, 240000, 10);
		specialeffect(EF_BLESSING, AREA, playerattached());
		sc_start(SC_BLESSING, 240000, 10);
		mes("^4d4dffThe Ship^000000 anchored in the southwest. It will take you to Izlude.");
		mes("Don't forget to ^4d4dffget some Lumber^000000 for the ship.");
		next();
		mes("[Captain Carocc]");
		mes("Wear your equipment.");
		mes("Your knife is more helpful than fists.");
		mes("Once you have the Lumber, talk to the crew.");
		close();
	case 3:
		break;
	}
	mes("[Captain Carocc]");
	mes("Everything is ready to sail over to Izlude.");
	mes("The ship is located at the south-western part of the island.");
	close();

OnInit:
	questinfo(QTYPE_QUEST, 1);
	setquestinfo(QINFO_JOB, Job_Novice);
	setquestinfo(QINFO_QUEST, 21002, 1);
	end;
}

//== Lumin (int_land) ==========================================
int_land,73,100,3	script	Lumin#iz_int	4_M_NOV_RUMIN,{
	if (Class == Job_Novice) {
		if (!questprogress(7471)) {
			cutin("nov_lumin01", 0);
			mes("[Lumin]");
			mes("............");
			mes("...?");
			next();
			cutin("fly_trock", 2);
			mes("[Captain Carocc]");
			mes("Hey, you awake now?");
			mes("That fella without much talk, I rescued him from the ocean.");
			next();
			mes("[Captain Carocc]");
			mes("Think he lost his memory.");
			mes("I gave him a new name 'Lumin'. But, just call him Lu...");
			next();
			mes("[Captain Carocc]");
			mes("And you can talk to other people like you just talked to me.");
			next();
			cutin("tutorial01", 3);
			mes("!- Information -!");
			mes("^4d4fffLeft mouse click allows basic game control.");
			mes("Such as conversation with NPC, movement, and attacks.^000000");
			next();
			cutin("fly_trock", 2);
			mes("[Captain Carocc]");
			mes("Many youngsters visit the ^4d4fffRune Midgard Kingdom^000000 to chase their dreams.");
			mes("Izlude is a satellite city of Prontera, the capital of the Rune Midgard Kingdom.");
			next();
			mes("[Captain Carocc]");
			mes("Izlude always welcomes(young adventurers like you who have just started their journey to the World.");
			mes("You are one of them of course.");
			next();
			mes("[Captain Carocc]");
			mes("I would like to tell you more stories, but it's time to get ready for arrival.");
			mes("There is an academy in Izlude for newcomers like yourself.");
			next();
			mes("[Captain Carocc]");
			mes("Let me give you more detail later.");
			mes("See you back at the harbor deck.");
			next();
			mes("[Captain Carocc]");
			mes("To get off this Island, you should enter the ^4d4fffShining Portal^000000 on the ship.");
			mes("All transportation is made through those portals.");
			setquest(7471);
			completequest(7471);
			next();
			cutin("nov_lumin01", 0);
			mes("[Lumin]");
			mes("Yes.");
			next();
			classchange(HIDDEN_WARP_NPC, 0);
			cutin("fly_trock", 2);
			mes("[Carocc]");
			mes("Oh, boy.");
			mes("What cute reaction.");
			close2();
			cutin("", 255);
			end;
		}
		cutin("nov_lumin01", 0);
		mes("[Lumin]");
		mes(".....");
		next();
		select("Should I introduce myself?", "My name is ~!");
		mesf("[%s]", strcharinfo(PC_NAME));
		mesf("I am %s!", strcharinfo(PC_NAME));
		next();
		mes("[Lu]");
		mes(".....");
		next();
		mes("[Lu]");
		mes(".....");
		mes("....So?");
		next();
		classchange(HIDDEN_WARP_NPC, 0);
		mes("- Lu just walked away with a cynical look on his face.");
		next();
		cutin("fly_trock", 2);
		mes("[Carocc]");
		mes("He's just shy, you know.");
		mes("You will see him again in Izlude, be good to him please.");
		next();
		mes("[Carocc]");
		mes("From now on, I'll be driving this ship around Izlude.");
		mes("Let's go, shall we?");
		close2();
		cutin("", 255);
		end;
	}
OnInit:
	questinfo(QTYPE_QUEST, 1);
	setquestinfo(QINFO_JOB, Job_Novice);
	setquestinfo(QINFO_QUEST, 7471, 1);
	end;
}
