//===== Hercules Script ======================================
//= Headgear to Costume converter >> Costume to Headgear converter
//===== By: ==================================================
//= Rebel, Zephyrus [rAthena]
//= Mhalicot [Hercules]
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= Hercules
//===== Description: =========================================
//= Allows a user to convert the equipped headgear
//	(on Top, Mid or Low) into a costume item.
//	It will remove any card and refine of the Item.
//= Allows a user to restore the equipped costume headgear
//	(on Top, Mid or Low) into its original form.
//	It will not return any card or refine of the item.
//===== Additional Comments: =================================
//= 1.0 Initial script [All of this script are credit to
//	Rebel, Zephyrus of [rAthena] and revised by [Mhalicot]
//	to make it compatible in Hercules.]
//= 1.1 Denied if Headgear is rental, has refine,
//	or has card[Mhalicot]
//	Note: You must apply the patch in order to use this script
//============================================================

function	script	checkZeny	{
	if (Zeny < 50000) {
		mes "[Coco Chanel]";
		mes "Oh I'm sorry it looks like you don't have enough zeny.  Please come back when you have 50,000.";
		close;
	}
	return;
}

-	script	Coco Chanel	-1,{
	mes "[Coco Chanel]";
	mes "I can convert your headgears into a costume or restore a costume its original form for only 50,000 zeny";
	switch(select("I want to convert.:I want to restore.:No thanks.")) {
		case 1:
			mesclear();
			checkZeny();
			mes "[Coco Chanel]";
			mes "What would you like to convert ?";
			mes "Refined and carded equipments are unable to be converted.";
			next;

			setarray .@Position$[1],"Top","Mid","Low";
			setarray .@Position[1],     1,    9,   10;
			set .@Menu$,"";

			for( set .@i, 1; .@i < 5; set .@i, .@i + 1 ) {
				if( getequipisequiped(.@Position[.@i]) )
				set .@Menu$, .@Menu$ + .@Position$[.@i] + "-" + "[" + getequipname(.@Position[.@i]) + "]";
				set .@Menu$, .@Menu$ + ":";
			}

			set .@Part, .@Position[ select(.@Menu$) ];
			set .@id, getequipid(.@Part);
			set .@ref, getequiprefinerycnt(.@Part);

			if( !getequipisequiped(.@Part) ) {
				mes "[Coco Chanel]";
				mes "You're not wearing anything there...";
				close;
			}

			mes "[Coco Chanel]";
			mes "You want to convert your " + getitemname(getequipid(.@Part)) + "?";
			next;

			if( select("Yes, proceed:No, I am sorry.") == 2 ) {
				mes "[Coco Chanel]";
				mes "Need some time to think about it, huh?";
				mes "Alright, I can understand.";
				close;
			}

			for(set .@i,0; .@i<4; set .@i,.@i+1)
				set .@slot[.@i], getequipcardid(.@Part,.@i);

			if (.@slot[0]>4000 && .@slot[0]<5000) { // If it has card don't convert
				mes "[Coco Chanel]";
				mes "A card? Here?!";
				mes "As I said before, I don't convert headgear with cards.";
				emotion e_hmm;
				close;
			}

			getinventorylist;
			for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1) {
				if (@inventorylist_expire[.@i] != 0 && @inventorylist_equip[.@i] != 0) { // If rental don't convert
					mes "[Coco Chanel]";
					mes "Sorry, I don't convert a rentals !";
					emotion e_hmm;
					close;
				}
			}

			if (.@ref > 0) { // If refine don't convert
				mes "[Coco Chanel]";
				mes "Sorry please keep in mind I don't convert refined headgear.";
				emotion e_hmm;
				close;
			}

			if (!countitem(.@id)) {
				mes "[Coco Chanel]";
				mes "Where is "+getitemname(@id)+"...?";
				npctalk "You're a snoozy cheater!";
				logmes "CHEATER: Tried to sign an item not having it: "+getitemname(@id);
				emotion e_wah;
				close;
			}

			costume .@Part; // Convert the Headgear
			mes "[Coco Chanel]";
			mes "Done, enjoy your costume !";
			Zeny -= 50000;
			close;
		case 2:
			mesclear();
			checkZeny();
			mes "Please, select what to restore.";
			mes "Remember, I will only restore it back without refine and cards.";
			next;

			setarray .@Position$[1],"Top","Mid","Low";
			setarray .@Position[1],     13,    12,   11;
			set .@Menu$,"";

			for( set .@i, 1; .@i < 5; set .@i, .@i + 1 ) {
				if( getequipisequiped(.@Position[.@i]) )
				set .@Menu$, .@Menu$ + .@Position$[.@i] + "-" + "[" + getequipname(.@Position[.@i]) + "]";
				set .@Menu$, .@Menu$ + ":";
			}

			set .@Part, .@Position[ select(.@Menu$) ];
			if( !getequipisequiped(.@Part) ) {
				mes "[Coco Chanel]";
				mes "Your not wearing anything there...";
				close;
			}

			mes "[Coco Chanel]";
			mes "You want to restore your " + getitemname(getequipid(.@Part)) + "?";
			next;

			if( select("Yes, proceed:No, I am sorry.") == 2 ) {
				mes "[Coco Chanel]";
				mes "Need some time to think about it, huh?";
				mes "Alright, I can understand.";
				close;
			}

			a = getequipid(.@Part);

			// the last two arguments are the card numbers generated by reserved_costume_id
			delitem2(a, 1, 1, 0, 0, 254, 0, 16958, 15);
			getitem a,1;

			mes "[Coco Chanel]";
			mes "Done, enjoy your restored headgear.";
			Zeny -= 50000;
			close;
		case 3:
			mes "[Coco Chanel]";
			mes "Very well. Return at once if you seek my services.";
			close;
	}
}

prontera,174,194,3	duplicate(Coco Chanel)	Coco Chanel#1	4_F_SHALOSH
