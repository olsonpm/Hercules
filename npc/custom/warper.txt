//===== Hercules Script ======================================
//= Warper
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.4b
//===== Description: =========================================
//= A complete - but very condensed - warper script.
//= Some coordinates written by Tekno-Kanix and ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Added missing duplicates and fixed coordinates.
//= 1.2 Added new episodes and simplified functions.
//= 1.3 Added Renewal checks and Instances menu.
//=     Aligned coordinates with @go.
//= 1.4 Added new Guild Dungeons.
//= 1.4a Slight edits.
//= 1.4b Added Wolfchev's Laboratory warp.
//============================================================

-	script	Warper	FAKE_NPC,{
function Go; function Disp; function Pick; function Restrict;

// --------------------------------------------------
//	Main Menu:
// --------------------------------------------------

menu	"Last Warp ^777777["+lastwarp$+"]^000000",-,
		" ~ Towns",Towns,
		" ~ Fields",Fields,
		" ~ Dungeons",Dungeons,
		" ~ Guild Castles",Castles,
		" ~ Guild Dungeons",Guild_Dungeons,
		" ~ Instances",Instances,
		" ~ Special Areas",Special;

	if (lastwarp$ == "")
		message strcharinfo(PC_NAME),"You haven't warped anywhere yet.";
	else
		warp lastwarp$,lastwarpx,lastwarpy;
	end;

// ------------------- Functions -------------------
// * Go("<map>",<x>,<y>);
//	~ Warps directly to a map.
//
// * Disp("<Menu Option>",<first option>,<last option>);
// * Pick("<map_prefix>"{,<index offset>});
//	~ Dynamic menu and map selection (auto-numbered).
//
// * Disp("<Option 1>:<Option 2>:<etc.>");
// * Pick("","<map1>","<map2>","<etc.>");
//	~ Manual menu and map selection (listed).
//
// * Restrict("<RE | Pre-RE>"{,<menu option numbers>});
//	~ Only allows map for Renewal or Pre-Renewal modes.
//     If menu option numbers are given, only those maps
//     will be restricted (i.e. not for "Go").
//
// Other notes:
//   ~ Array @c[] holds all (x,y) coordinates.
//   ~ Use @c[2] EXCEPT when maps begin dynamically
//	  at 0: use @c[0] and Pick() offset 1.
// --------------------------------------------------

function Go {
	lastwarp$ = getarg(0);
	lastwarpx = getarg(1,0);
	lastwarpy = getarg(2,0);
	warp getarg(0),getarg(1,0),getarg(2,0);
	end;
}
function Disp {
	if (getargcount() < 3)
		@menu$ = getarg(0);
	else {
		@menu$ = "";
		for (.@i = getarg(1); .@i <= getarg(2); .@i++)
			@menu$ = @menu$+getarg(0)+" "+.@i+":";
	}
	return;
}
function Pick {
	.@warp_block = @warp_block;
	@warp_block = 0;
	.@select = select(@menu$);
	if (getarg(0) == "") {
		.@i = .@select;
		.@map$ = getarg(.@i);
	} else {
		.@i = .@select-getarg(1,0);
		.@map$ = getarg(0)+((.@i<10)?"0":"")+.@i;
	}
	if (.@warp_block & (1<<.@select)) {
		message strcharinfo(PC_NAME),"This map is not enabled in "+(RENEWAL?"":"Pre-")+"Renewal.";
		end;
	}
	.@x = @c[.@i*2];
	.@y = @c[.@i*2+1];
	deletearray @c[0],getarraysize(@c);
	Go(.@map$,.@x,.@y);
}
function Restrict {
	if ((getarg(0) == "RE" && !RENEWAL) || (getarg(0) == "Pre-RE" && RENEWAL)) {
		if (getarg(1,0)) {
			@warp_block = 0;
			for (.@i = 1; .@i < getargcount(); .@i++)
				@warp_block = @warp_block | (1<<getarg(.@i));
		} else {
			message strcharinfo(PC_NAME),"This map is not enabled in "+(RENEWAL?"":"Pre-")+"Renewal.";
			end;
		}
	}
	return;
}

// --------------------------------------------------
	Towns:
// --------------------------------------------------
menu	"Prontera",T1, "Alberta",T2, "Aldebaran",T3, "Amatsu",T4, "Ayothaya",T5,
		"Comodo",T6, "Einbech",T7, "Einbroch",T8, "Geffen",T9, "Gonryun",T10,
		"Hugel",T11, "Izlude",T12, "Jawaii",T13, "Lighthalzen",T14, "Louyang",T15,
		"Lutie",T16, "Manuk",T17, "Midgarts Expedition Camp",T18, "Morroc",T19,
		"Moscovia",T20, "Nameless Island",T21, "Niflheim",T22, "Payon",T23, "Rachel",T24,
		"Splendide",T25, "Thor Camp",T26, "Umbala",T27, "Veins",T28, "Yuno",T29;

T1: Go("prontera",155,183);
T2: Go("alberta",28,234);
T3: Go("aldebaran",140,131);
T4: Go("amatsu",198,84);
T5: Go("ayothaya",208,166);
T6: Go("comodo",209,143);
T7: Go("einbech",63,35);
T8: Go("einbroch",64,200);
T9: Go("geffen",119,59);
T10: Go("gonryun",160,120);
T11: Go("hugel",96,145);
T12: Go("izlude",128,(RENEWAL?146:114));
T13: Go("jawaii",251,132);
T14: Go("lighthalzen",158,92);
T15: Go("louyang",217,100);
T16: Go("xmas",147,134);
T17: Go("manuk",282,138);
T18: Go("mid_camp",210,288);
T19: Go("morocc",156,93);
T20: Go("moscovia",223,184);
T21: Go("nameless_n",256,215);
T22: Go("niflheim",202,174);
T23: Go("payon",179,100);
T24: Go("rachel",130,110);
T25: Go("splendide",201,147);
T26: Go("thor_camp",246,68);
T27: Go("umbala",97,153);
T28: Go("veins",216,123);
T29: Go("yuno",157,51);

// --------------------------------------------------
	Fields:
// --------------------------------------------------
menu	"Amatsu Fields",F1, "Ayothaya Fields",F2, "Comodo Fields",F3, "Einbroch Fields",F4,
		"El Dicastes Fields",F5, "Geffen Fields",F6, "Gonryun Fields",F7, "Hugel Fields",F8,
		"Lighthalzen Fields",F9, "Louyang Field",F10, "Lutie Field",F11, "Manuk Fields",F12,
		"Mjolnir Fields",F13, "Moscovia Fields",F14, "Niflheim Fields",F15, "Payon Forests",F16,
		"Prontera Fields",F17, "Rachel Fields",F18, "Sograt Deserts",F19, "Splendide Fields",F20,
		"Umbala Fields",F21, "Veins Fields",F22, "Yuno Fields",F23;

F1: setarray @c[2],190,197;
	Disp("Amatsu Field",1,1); Pick("ama_fild");
F2: setarray @c[2],173,134,212,150;
	Disp("Ayothaya Field",1,2); Pick("ayo_fild");
F3: setarray @c[2],180,178,231,160,191,172,228,194,224,203,190,223,234,177,194,175,172,172;
	Disp("Comodo Field",1,9); Pick("cmd_fild");
F4: setarray @c[2],142,225,182,141,187,228,185,173,216,173,195,148,272,220,173,214,207,174,196,200;
	Disp("Einbroch Field",1,10); Pick("ein_fild");
F5: setarray @c[2],143,132,143,217;
	Disp("El Dicastes Field",1,2); Pick("dic_fild");
F6: setarray @c[0],46,199,213,204,195,212,257,192,188,171,166,263,248,158,195,191,186,183,221,117,178,218,136,328,240,181,235,235,211,185;
	 Disp("Geffen Field",0,14); Pick("gef_fild",1);
F7: setarray @c[2],220,227;
	 Disp("Gonryun Field",1,1); Pick("gon_fild");
F8: setarray @c[2],268,101,222,193,232,185,252,189,196,106,216,220,227,197;
	 Disp("Hugel Field",1,7); Pick("hu_fild");
F9: setarray @c[2],240,179,185,235,240,226;
	 Disp("Lighthalzen Field",1,3); Pick("lhz_fild");
F10: setarray @c[2],229,187;
	 Disp("Louyang Field",1,1); Pick("lou_fild");
F11: setarray @c[2],115,145;
	 Disp("Lutie Field",1,1); Pick("xmas_fild");
F12: setarray @c[2],35,236,35,262,84,365;
	 Disp("Manuk Field",1,3); Pick("man_fild");
F13: setarray @c[2],204,120,175,193,208,213,179,180,181,240,195,270,235,202,188,215,205,144,245,223,180,206,196,208;
	 Disp("Mjolnir Field",1,12); Pick("mjolnir_");
F14: setarray @c[2],82,104,131,147;
	 Disp("Moscovia Field",1,2); Pick("mosk_fild");
F15: setarray @c[2],215,229,167,234;
	 Disp("Niflheim Field",1,2); Pick("nif_fild");
F16: setarray @c[2],158,206,151,219,205,148,186,247,134,204,193,235,200,177,137,189,201,224,160,205,194,150;
	 Disp("Payon Forest",1,11); Pick("pay_fild");
F17: setarray @c[0],208,227,190,206,240,206,190,143,307,252,239,213,185,188,193,194,187,218,210,183,195,149,198,164;
	 Disp("Prontera Field",0,11); Pick("prt_fild",1);
F18: setarray @c[2],192,162,235,166,202,206,202,208,225,202,202,214,263,196,217,201,87,121,277,181,221,185,175,200,174,197;
	 Disp("Rachel Field",1,13); Pick("ra_fild");
F19: setarray @c[2],219,205,177,206,194,182,224,170,198,216,156,187,185,263,206,228,208,238,209,223,85,97,207,202,31,195,38,195;
	 Disp("Sograt Desert 1:Sograt Desert 2:Sograt Desert 3:Sograt Desert 7:Sograt Desert 11:Sograt Desert 12:Sograt Desert 13:Sograt Desert 16:Sograt Desert 17:Sograt Desert 18:Sograt Desert 19:Sograt Desert 20:Sograt Desert 21:Sograt Desert 22");
	 Pick("","moc_fild01","moc_fild02","moc_fild03","moc_fild07","moc_fild11","moc_fild12","moc_fild13","moc_fild16","moc_fild17","moc_fild18","moc_fild19","moc_fild20","moc_fild21","moc_fild22");
F20: setarray @c[2],175,186,236,184,188,204;
	 Disp("Splendide Field",1,3); Pick("spl_fild");
F21: setarray @c[2],217,206,223,221,237,215,202,197;
	 Disp("Umbala Field",1,4); Pick("um_fild");
F22: setarray @c[2],186,175,196,370,222,45,51,250,202,324,150,223,149,307;
	 Disp("Veins Field",1,7); Pick("ve_fild");
F23: setarray @c[2],189,224,192,207,221,157,226,199,223,177,187,232,231,174,196,203,183,214,200,124,195,226,210,304;
	 Disp("Yuno Field",1,12); Pick("yuno_fild");

// --------------------------------------------------
	Dungeons:
// --------------------------------------------------
menu	"Abyss Lakes",D1, "Amatsu Dungeon",D2, "Anthell",D3, "Ayothaya Dungeon",D4,
		"Beach Dungeon",D5, "Clock Tower",D6, "Coal Mines",D7, "Culvert",D8,
		"Cursed Abbey",D9, "Einbroch Dungeon",D10, "Gefenia",D11, "Geffen Dungeon",D12,
		"Glast Heim",D13, "Gonryun Dungeon",D14, "Hidden Dungeon",D15, "Ice Dungeon",D16,
		"Juperos",D17, "Kiel Dungeon",D18, "Louyang Dungeon",D19, "Magma Dungeon",D20,
		"Moscovia Dungeon",D21, "Nidhogg's Dungeon",D22, "Odin Temple",D23, "Orc Dungeon",D24,
		"Payon Dungeon",D25, "Rachel Sanctuary",D26, "Sphinx",D27, "Sunken Ship",D28, "Thanatos Tower",D29,
		"Thor Volcano",D30, "Toy Factory",D31, "Turtle Dungeon",D32, "Umbala Dungeon",D33;

D1: setarray @c[2],261,272,275,270,116,27;
	Disp("Abyss Lakes",1,3); Pick("abyss_");
D2: setarray @c[2],228,11,34,41,119,14;
	Disp("Amatsu Dungeon",1,3); Pick("ama_dun");
D3: setarray @c[2],35,262,168,170;
	Disp("Anthell",1,2); Pick("anthell");
D4: setarray @c[2],275,19,24,26;
	Disp("Ancient Shrine Maze:Inside Ancient Shrine"); Pick("ayo_dun");
D5: setarray @c[2],266,67,255,244,23,260;
	Disp("Beach Dungeon",1,3); Pick("","beach_dun","beach_dun2","beach_dun3");
D6: setarray @c[2],199,159,148,283,65,147,56,155,297,25,127,169,277,178,268,74;
	Disp("Clock Tower 1:Clock Tower 2:Clock Tower 3:Clock Tower 4:Basement 1:Basement 2:Basement 3:Basement 4");
	Pick("","c_tower1","c_tower2","c_tower3","c_tower4","alde_dun01","alde_dun02","alde_dun03","alde_dun04");
D7: setarray @c[2],52,17,381,343,302,262;
	 Disp("Coal Mines",1,3); Pick("mjo_dun");
D8: setarray @c[2],131,247,19,19,180,169,100,92;
	 Disp("Culvert",1,4); Pick("","prt_sewb1","prt_sewb2","prt_sewb3","prt_sewb4");
D9: setarray @c[2],51,14,150,11,120,10;
	 Disp("Cursed Abbey",1,3); Pick("abbey");
D10: setarray @c[2],22,14,292,290;
	 Disp("Einbroch Dungeon",1,2); Pick("ein_dun");
D11: setarray @c[2],40,103,203,34,266,168,130,272;
	 Disp("Gefenia",1,4); Pick("gefenia",0);
D12: setarray @c[0],104,99,115,236,106,132,203,200;
	 Disp("Geffen Dungeon",1,4); Pick("gef_dun",1);
D13: setarray @c[2],370,304,199,29,104,25,150,15,157,287,147,15,258,255,108,291,171,283,68,277,156,7,12,7,133,271,224,274,14,70,150,14;
	 Disp("Entrance:Castle 1:Castle 2:Chivalry 1:Chivalry 2:Churchyard:Culvert 1:Culvert 2:Culvert 3:Culvert 4:St. Abbey:Staircase Dungeon:Underground Cave 1:Underground Cave 2:Underground Prison 1:Underground Prison 2");
	 Pick("","glast_01","gl_cas01","gl_cas02","gl_knt01","gl_knt02","gl_chyard","gl_sew01","gl_sew02","gl_sew03","gl_sew04","gl_church","gl_step","gl_dun01","gl_dun02","gl_prison","gl_prison1");
D14: setarray @c[2],153,53,28,113,68,16;
	 Disp("Gonryun Dungeon",1,3); Pick("gon_dun");
D15: setarray @c[2],176,7,93,20,23,8;
	 Disp("Hidden Dungeon",1,3); Pick("prt_maze");
D16: setarray @c[2],157,14,151,155,149,22,33,158;
	 Disp("Ice Dungeon",1,4); Pick("ice_dun");
D17: setarray @c[2],140,51,53,247,37,63,150,285;
	 Disp("Entrance:Juperos 1:Juperos 2:Core");
	 Pick("","jupe_cave","juperos_01","juperos_02","jupe_core");
D18: setarray @c[2],28,226,41,198;
	 Disp("Kiel Dungeon",1,2); Pick("kh_dun");
D19: setarray @c[2],218,196,282,20,165,38;
	 Disp("The Royal Tomb:Inside the Royal Tomb:Suei Long Gon"); Pick("lou_dun");
D20: setarray @c[2],126,68,47,30;
	 Disp("Magma Dungeon",1,2); Pick("mag_dun");
D21: setarray @c[2],189,48,165,30,32,135;
	 Disp("Moscovia Dungeon",1,3); Pick("mosk_dun");
D22: setarray @c[2],61,239,60,271;
	 Disp("Nidhogg's Dungeon",1,2); Pick("nyd_dun");
D23: setarray @c[2],298,167,224,149,266,280;
	 Disp("Odin Temple",1,3); Pick("odin_tem");
D24: setarray @c[2],32,170,21,185;
	 Disp("Orc Dungeon",1,2); Pick("orcsdun");
D25: setarray @c[0],21,183,19,33,19,63,155,159,201,204;
	 Disp("Payon Dungeon",1,5); Pick("pay_dun",1);
D26: setarray @c[2],140,11,32,21,8,149,204,218,150,9;
	 Disp("Rachel Sanctuary",1,5); Pick("ra_san");
D27: setarray @c[2],288,9,149,81,210,54,10,222,100,99;
	 Disp("Sphinx",1,5); Pick("","in_sphinx1","in_sphinx2","in_sphinx3","in_sphinx4","in_sphinx5");
D28: setarray @c[2],69,24,102,27;
	 Disp("Sunken Ship",1,2); Pick("treasure");
D29: setarray @c[2],150,39,150,136,220,158,59,143,62,11,89,221,35,166,93,148,29,107,159,138,19,20,130,52;
	 Disp("Thanatos Tower",1,12); Pick("tha_t");
D30: setarray @c[2],21,228,75,205,34,272;
	 Disp("Thor Volcano",1,3); Pick("thor_v");
D31: setarray @c[2],205,15,129,133;
	 Disp("Toy Factory",1,2); Pick("xmas_dun");
D32: setarray @c[2],154,49,148,261,132,189,100,192;
	 Disp("Entrance:Turtle Dungeon 1:Turtle Dungeon 2:Turtle Dungeon 3"); Pick("tur_dun");
D33: setarray @c[2],42,31,48,30,204,78;
	 Disp("Carpenter's Shop in the Tree:Passage to a Foreign World:Hvergermil's Fountain");
	 Pick("","um_dun01","um_dun02","yggdrasil01");

// --------------------------------------------------
	Castles:
// --------------------------------------------------
menu	"Aldebaran Castles",C1, "Geffen Castles",C2, "Payon Castles",C3,
		"Prontera Castles",C4, "Arunafeltz Castles",C5, "Schwaltzvalt Castles",C6;

C1: setarray @c[2],48,83,95,249,142,85,239,242,264,90;
	Disp("Neuschwanstein:Hohenschwangau:Nuenberg:Wuerzburg:Rothenburg");
	Pick("","alde_gld","alde_gld","alde_gld","alde_gld","alde_gld");
C2: setarray @c[2],214,75,308,240,143,240,193,278,305,87;
	Disp("Repherion:Eeyolbriggar:Yesnelph:Bergel:Mersetzdeitz");
	Pick("","gef_fild13","gef_fild13","gef_fild13","gef_fild13","gef_fild13");
C3: setarray @c[2],121,233,295,116,317,293,140,160,204,266;
	Disp("Bright Arbor:Scarlet Palace:Holy Shadow:Sacred Altar:Bamboo Grove Hill");
	Pick("","pay_gld","pay_gld","pay_gld","pay_gld","pay_gld");
C4: setarray @c[2],134,65,240,128,153,137,111,240,208,240;
	Disp("Kriemhild:Swanhild:Fadhgridh:Skoegul:Gondul");
	Pick("","prt_gld","prt_gld","prt_gld","prt_gld","prt_gld");
C5: setarray @c[2],158,272,83,47,68,155,299,345,292,107;
	Disp("Mardol:Cyr:Horn:Gefn:Banadis");
	Pick("","aru_gld","aru_gld","aru_gld","aru_gld","aru_gld");
C6: setarray @c[2],293,100,288,252,97,196,137,90,71,315;
	Disp("Himinn:Andlangr:Viblainn:Hljod:Skidbladnir");
	Pick("","sch_gld","sch_gld","sch_gld","sch_gld","sch_gld");

// --------------------------------------------------
	Guild_Dungeons:
// --------------------------------------------------
menu	"Arunafeltz",G5, "Schwaltzvalt",G6;

G1: Go("arug_dun01",199,195);
G2: Go("schg_dun01",200,124);

// --------------------------------------------------
	Instances:
// --------------------------------------------------
menu	"Endless Tower",I1, "Nidhoggur's Nest",I2, "Orc's Memory",I3, "Sealed Shrine",I4;

I1: Go("e_tower",72,112);
I2: Go("nyd_dun02",95,193);
I3: Go("gef_fild10",240,198);
I4: Go("monk_test",306,143);

// --------------------------------------------------
	Special:
// --------------------------------------------------
menu	"Auction Hall",S1, "Battlegrounds",S2, "Casino",S3, "Gonryun Arena",S4,
		"Izlude Arena",S5, "Monster Race Arena",S6, "Turbo Track",S7;

S1: Go("auction_01",22,68);
S2: Go("bat_room",154,150);
S3: Go("cmd_in02",179,129);
S4: Go("gon_test",48,10);
S5: Go("arena_room",100,88);
S6: Go("p_track01",62,41);
S7: Go("turbo_room",99,114);
}

// --------------------------------------------------
//	Duplicates:
// --------------------------------------------------
alb2trea,57,70,6	duplicate(Warper)	Warper#tre	4_F_VALKYRIE
alberta,28,240,4	duplicate(Warper)	Warper#alb	4_F_VALKYRIE
aldebaran,145,118,4	duplicate(Warper)	Warper#alde	4_F_VALKYRIE
amatsu,203,87,4	duplicate(Warper)	Warper#ama	4_F_VALKYRIE
ayothaya,209,169,6	duplicate(Warper)	Warper#ayo	4_F_VALKYRIE
comodo,194,158,4	duplicate(Warper)	Warper#com	4_F_VALKYRIE
einbech,59,38,6	duplicate(Warper)	Warper#einbe	4_F_VALKYRIE
einbroch,69,202,4	duplicate(Warper)	Warper#einbr	4_F_VALKYRIE
gef_fild10,71,339,4	duplicate(Warper)	Warper#orc	4_F_VALKYRIE
geffen,124,72,4	duplicate(Warper)	Warper#gef	4_F_VALKYRIE
glast_01,372,308,4	duplicate(Warper)	Warper#glh	4_F_VALKYRIE
gonryun,162,122,4	duplicate(Warper)	Warper#gon	4_F_VALKYRIE
hugel,101,151,4	duplicate(Warper)	Warper#hug	4_F_VALKYRIE
izlu2dun,110,92,4	duplicate(Warper)	Warper#izd	4_F_VALKYRIE
izlude,134,150,4	duplicate(Warper)	Warper#izl	4_F_VALKYRIE	//Pre-RE: (132,120)
jawaii,253,138,4	duplicate(Warper)	Warper#jaw	4_F_VALKYRIE
lighthalzen,162,102,4	duplicate(Warper)	Warper#lhz	4_F_VALKYRIE
louyang,208,103,6	duplicate(Warper)	Warper#lou	4_F_VALKYRIE
manuk,274,146,6	duplicate(Warper)	Warper#man	4_F_VALKYRIE
mid_camp,216,288,4	duplicate(Warper)	Warper#mid	4_F_VALKYRIE
mjolnir_02,85,364,4	duplicate(Warper)	Warper#mjo	4_F_VALKYRIE
moc_ruins,64,164,4	duplicate(Warper)	Warper#moc	4_F_VALKYRIE
morocc,159,97,4	duplicate(Warper)	Warper#mor	4_F_VALKYRIE
moscovia,229,191,4	duplicate(Warper)	Warper#mos	4_F_VALKYRIE
nameless_n,259,213,4	duplicate(Warper)	Warper#nam	4_F_VALKYRIE
niflheim,205,179,4	duplicate(Warper)	Warper#nif	4_F_VALKYRIE
pay_arche,42,134,4	duplicate(Warper)	Warper#arc	4_F_VALKYRIE
payon,182,108,4	duplicate(Warper)	Warper#pay	4_F_VALKYRIE
prontera,159,192,4	duplicate(Warper)	Warper#prt	4_F_VALKYRIE
prt_fild05,279,223,6	duplicate(Warper)	Warper#cul	4_F_VALKYRIE
rachel,135,116,4	duplicate(Warper)	Warper#rac	4_F_VALKYRIE
splendide,205,153,4	duplicate(Warper)	Warper#spl	4_F_VALKYRIE
thor_camp,249,76,4	duplicate(Warper)	Warper#thor	4_F_VALKYRIE
umbala,106,150,3	duplicate(Warper)	Warper#umb	4_F_VALKYRIE
veins,214,123,4	duplicate(Warper)	Warper#ve	4_F_VALKYRIE
xmas,150,136,6	duplicate(Warper)	Warper#xmas	4_F_VALKYRIE
yuno,162,47,4	duplicate(Warper)	Warper#yuno	4_F_VALKYRIE

// --------------------------------------------------
//	Duplicates (Renewal):
// --------------------------------------------------
brasilis,201,222,4	duplicate(Warper)	Warper#bra	4_F_VALKYRIE
dewata,204,186,6	duplicate(Warper)	Warper#dew	4_F_VALKYRIE
dicastes01,194,194,6	duplicate(Warper)	Warper#dic	4_F_VALKYRIE
ecl_in01,51,60,4	duplicate(Warper)	Warper#ecl	4_F_VALKYRIE
malangdo,134,117,6	duplicate(Warper)	Warper#mal	4_F_VALKYRIE
malaya,231,204,4	duplicate(Warper)	Warper#ma	4_F_VALKYRIE
mora,57,152,4	duplicate(Warper)	Warper#mora	4_F_VALKYRIE
